# AtomicAndPhysicalConstants/src/species.jl


"""
    subatomic_particle(name::String)

## Description:
sub-constructor for struct Species: subatomic_particle generates a Species object 
for a particle with openPMD identifier 'name'
"""
function subatomic_particle(name::String)::Species
  # write the particle out directly

  particle = SUBATOMIC_SPECIES[name]
  
  name == "photon" && return Species(
    name, 
    particle.charge,
    particle.mass,
    particle.spin,
    particle.gspin,
    particle.moment,
    Int(0), 
    Kind.PHOTON)
  name in leptons && return Species(
    name, 
    particle.charge,
    particle.mass,
    particle.spin,
    particle.gspin,
    particle.moment,
    0, 
    Kind.LEPTON)

  return Species(
    name, 
    particle.charge,
    particle.mass,
    particle.spin,
    particle.gspin,
    particle.moment,
    0, 
    Kind.HADRON)

end


"""
    atomic_particle(name::String, charge::Float64, iso::Int)

## Description:
sub-constructor for struct Species: atomic_particle generates a Species object 
for an atom with atomic symbol 'name', charge state 'charge', and mass number 'iso'
## fields:
- `name::String':         the atomic symbol, must be exact. anti-prefix specifies whether it is an anti-atom
- `charge::Float64':      the net charge of the particle in units of [e]
- `iso::Int':             the mass number of the isotope, -1 for the most abundant isotope
"""
function atomic_particle(name::String, charge::Float64, iso::Int;)

  # whether the atom is anti-atom
  anti_atom::Bool = occursin(anti_regEx, name)
  # if the particle is an anti-particle, remove the prefix for easier lookup
  AS::String = replace(name, anti_regEx => "")

  haskey(ATOMIC_SPECIES, AS) || error("$AS is not a valid atomic species")

  # grab the particular element from the stack
  atom::AtomicSpecies = ATOMIC_SPECIES[AS]
  # convert the mass of the selected isotope from amu to MeV
  nmass::Float64 = atom.mass[iso] * EV_PER_AMU

  spin::Float64 = 0.0

  mass::Float64 = begin
    if anti_atom == false
      nmass + SUBATOMIC_SPECIES["electron"].mass * (-charge)
      # for a nominal atom, add 1 electron mass for every - charge
    else
      nmass + SUBATOMIC_SPECIES["positron"].mass * charge
      # for an anti-atom, add 1 positron mass for every + charge
    end
  end
  if iso == -1 # if it's the average, make an educated guess at the spin
    partonum::Float64 = round(atom.mass[iso])
    if anti_atom == false
      spin = 0.5 * (partonum + (atom.Z - charge))
    else
      spin = 0.5 * (partonum + (atom.Z + charge))
    end
  else # otherwise, use the sum of proton and neutron spins
    spin = 0.5 * iso
  end
  # return the object to track
  if anti_atom == false
    return Species(
      AS, 
      charge, 
      mass,
      spin, 
      0.0,
      0.0, 
      iso, 
      Kind.ATOM
    )
  else
    return Species(
      "anti-" * AS, 
      charge, 
      mass,
      spin, 
      0.0,
      0.0, 
      iso, 
      Kind.ATOM
    )
  end

end




# vector of Null names
const nulls::Vector{String} = ["NULL", "Null", "null", ""]


#####################################################################
#####################################################################



function Species(speciesname::String)
  
  # if the name is "Null", return a null Species
  if speciesname in nulls
    return Species()
  end


  for (k, _) in SUBATOMIC_SPECIES
    # whether the particle is in the subatomic species dictionary
    if k == speciesname
      return subatomic_particle(k)
    end
  end

  anti = occursin(anti_regEx, speciesname)
  # # if the particle is an anti-particle, remove the prefix for easier lookup
  !anti ? (name = speciesname) : (name = replace(speciesname, anti_regEx => ""))

  # A leading run of Unicode superscript digits denotes the mass number (e.g. "⁴He").
  # Rewrite it to the canonical `#`-prefixed form so that a mass number given as
  # plain ASCII digits always requires an explicit `#` (e.g. "#4He", not "4He").
  if !isempty(name) && haskey(SUPERSCRIPT_MAP, first(name))
    name = "#" * name
  end

  name = normalize_superscripts(name)

  # The `#` is mandatory whenever an (ASCII) mass number is given.
  m = match(r"^(?:#(\d+))?([A-Z][a-z]?)([+-].*)?$", name)   # This captures iso, species, charge as m.captures[1,2,3]

  # Parse species
  (m !== nothing && haskey(ATOMIC_SPECIES, m.captures[2])) ||
    error("you did not specify an atomic species or subatomic species in $speciesname")

  atom::String = m.captures[2]

  # Parse iso
  iso::Int = -1
  if m.captures[1] != ""
    iso = m.captures[1] === nothing ? -1 : parse(Int, m.captures[1])
    haskey(ATOMIC_SPECIES[atom].mass, iso) ||
      error("$iso is not a valid isotope of $atom")
  end

  # Parse charge
  charge_str::String = m.captures[3] === nothing ? "" : m.captures[3]
  !(occursin('+', charge_str) && occursin('-', charge_str)) ||
    error("$speciesname has an ambiguously defined charge value.")
  charge::Int = chargeparse(charge_str)
  
  (!anti && charge > ATOMIC_SPECIES[atom].Z )&& error("The specified element cannot have the specified charge.")
  (anti && charge < -ATOMIC_SPECIES[atom].Z) && error("The specified element cannot have the specified charge.")

  anti ? (return atomic_particle("anti-" * atom, Float64(charge), iso)) : (return atomic_particle(atom, Float64(charge), iso))
  


end
